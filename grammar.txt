# General

<optional-spaces>
    <empty>
    <space-token> <optional-spaces>

# Units

<unit-of-measure>
    <optional-spaces> <internal-unit>
    <optional-true> <physical-unit>

# Number literals

<plus-or-minus>
    (+_12)
    (-_12)

<number>
    <optional-signs> <unsigned-number>

<optional-signs>
    <optional-spaces>
    <optional-signs> <plus-or-minus> <optional-spaces>

<unsigned-number>
    <normal-integer>
    <coerced-integer>

<normal-integer>
    <internal-integer>
    <integer-constant> <one-optional-space>
    ('_12) <octal-constant> <one-optional-space>
    ("_12) <hexadecimal-constant> <one-optional-space>
    (`_12) <character-token> <one-optional-space>

<integer-constant>
    <digit>
    <digit> <integer-constant>

<octal-constant>
    <octal-digit>
    <octal-digit> <octal-constant>

<hexadecimal-constant>
    <hex-digit>
    <hex-digit> <hexadecimal-constant>

<octal-digit>
    (0_12)
    (1_12)
    (2_12)
    (3_12)
    (4_12)
    (5_12)
    (6_12)
    (7_12)

<digit>
    <octal-digit>
    (8_12)
    (9_12)

<hex-digit>
    <digit>
    A(_11)
    B(_11)
    C(_11)
    D(_11)
    E(_11)
    F(_11)
    (A_12)
    (B_12)
    (C_12)
    (D_12)
    (E_12)
    (F_12)

<one-optional-space>
    <space-token>
    <empty>

<coerced-integer>
    <internal-dimen>
    <internal-glue>

# Lengths ('dimen' and 'mudimen')

<dimen>
    <optional-signs> <unsigned-dimen>

<unsigned-dimen>
    <normal-dimen>
    <coerced-dimen>

<coerced-dimen>
    <internal-glue>

<normal-dimen>
    <internal-dimen>
    <factor> <unit-of-measure>

<factor>
    <normal-integer>
    <decimal-constant>

<decimal-constant>
    (._12)
    (,_12)
    <digit> <decimal-constant>
    <decimal-constant> <digit>

<unit-of-measure>
    <optional-spaces> <internal-unit>
    <optional-true> <physical unit> <one-optional-space>

<internal-unit>
    [em] <one-optional-space>
    [ex] <one-optional-space>
    <internal-integer>
    <internal-dimen>
    <internal-glue>

<optional-true>
    [true]
    <empty>

<physical unit>
    [pt]
    [pc]
    [in]
    [bp]
    [cm]
    [mm]
    [dd]
    [cc]
    [sp]

<mudimen>
    <optional-signs> <unsigned mudimen>

<unsigned mudimen>
    <normal-mudimen>
    <coerced-mudimen>

<coerced-mudimen>
    <internal-muglue>

<normal-mudimen>
    <factor> <mu-unit>

<mu-unit>
    <optional-spaces> <internal-muglue>
    [mu] <one-optional-space>

# Glue

<glue>
    <optional-signs> <internal-glue>
    <dimen> <stretch> <shrink>

<stretch>
    [plus] <dimen>
    [plus] <fil-dimen>
    <optional-spaces>

<shrink>
    [minus] <dimen>
    [minus] <fil-dimen>
    <optional-spaces>

<fil-dimen>
    <optional-signs> <factor> <fil-unit> <optional-spaces>

<fil-unit>
    [fil]
    <fil-unit> [l]

<muglue>
    <optional-signs> <internal-muglue>
    <mudimen> <mustretch> <mushrink>

<mustretch>
    [plus] <mudimen>
    [plus] <fil-dimen>
    <optional-spaces>

<mushrink>
    [minus] <mudimen>
    [minus] <fil-dimen>
    <optional-spaces>

# Internal quantities

<internal-integer>
    <integer-parameter>
    <special-integer>
    \lastpenalty
    <countdef-token>
    \count <8-bit-number>
    <codename> <8-bit-number>
    <chardef-token>
    <mathchardef-token>
    \parshape
    \inputlineno
    \hyphenchar <font>
    \skewchar <font>
    \badness

<special-integer>
    \spacefactor
    \prevgraf
    \deadcycles
    \insertpenalties

<codename>
    \catcode
    \mathcode
    \lccode
    \uccode
    \sfcode
    \delcode

<font>
    <fontdef-token>
    \font
    <family-member>

<family-member>
    <font-range> <4-bit-number>

<font-range>
    \textfont
    \scriptfont
    \scriptscriptfont

<internal-dimen>
    <dimen-parameter>
    <special-dimen>
    \lastkern
    <dimendef-token>
    \dimen <8-bit-number>
    <box-dimension> <8-bit-number>
    \fontdimen <number> <font>

<special-dimen>
    \prevdepth
    \pagegoal
    \pagetotal
    \pagestretch
    \pagefilstretch
    \pagefillstretch
    \pagefilllstretch
    \pageshrink
    \pagedepth

<box-dimension>
    \ht
    \wd
    \dp

<internal-glue>
    <glue-parameter>
    \lastskip
    <skipdef-token>
    \skip <8-bit-number>

<internal-muglue>
    <muglue-parameter>
    \lastskip
    <muskipdef-token>
    \muskip <8-bit-number>

## Assignments

<assignment>
    <non-macro-assignment>
    <macro-assignment>

<non-macro-assignment>
    <simple-assignment>
    \global <non-macro-assignment>

<macro-assignment>
    <definition>
    <prefix> <macro-assignment>

<prefix>
    \global
    \long
    \outer

<equals>
    <optional-spaces>
    <optional-spaces> (=_12)

## Macro definitions

<definition>
    <def> <control-sequence> <definition-text>

<def>
    \def
    \gdef
    \edef
    \xdef

<definition-text>
    <parameter-text> <left brace> <balanced-text> <right-brace>

<filler>
    <optional-spaces>
    <filler> \relax <optional-spaces>

<general-text>
    <filler> { <balanced-text> <right-brace>

## Non-Macro assignments

<simple-assignment>
    <variable-assignment>
    <arithmetic>
    <code-assignment>
    <let-assignment>
    <shorthand-definition>
    <fontdef-token>
    <family-assignment>
    <shape-assignment>
    \read <number> [to] <optional-spaces> <control-sequence>
    \setbox <8-bit-number> <equals> <filler> <box>
    \font <control-sequence> <equals> <file-name> <at-clause>
    <global-assignment>

<variable-assignment>
    <integer-variable> <equals> <number>
    <dimen-variable> <equals> <dimen>
    <glue-variable> <equals> <glue>
    <muglue-variable> <equals> <muglue>
    <token-variable> <equals> <general-text>
    <token-variable> <equals> <filler> <token-variable>

<arithmetic>
    \advance <integer-variable> <optional-by> <number>
    \advance <dimen-variable> <optional-by> <dimen>
    \advance <glue-variable> <optional-by> <glue>
    \advance <muglue-variable> <optional-by> <muglue>
    \multiply <numeric-variable> <optional-by> <number>
    \divide <numeric-variable> <optional-by> <number>

<optional-by>
    [by]
    <optional-spaces>

<integer-variable>
    <integer-parameter>
    <countdef-token>
    \count <8-bit-number>

<dimen-variable>
    <dimen-parameter>
    <dimendef-token>
    \dimen <8-bit-number>

<glue-variable>
    <glue-parameter>
    <skipdef-token>
    \skip <8-bit-number>

<muglue-variable>
    <muglue-parameter>
    <muskipdef-token>
    \muskip <8-bit-number>

<token-variable>
    <token parameter>
    <toksdef-token>
    \toks <8-bit-number>

<numeric-variable>
    <integer-variable>
    <dimen-variable>
    <glue-variable>
    <muglue-variable>

<at-clause>
    [at] <dimen>
    [scaled] <number>
    <optional-spaces>

<code-assignment>
    <codename> <8-bit-number> <equals> <number>

<let-assignment>
    \futurelet <control-sequence> <token> <token>
    \let <control-sequence> <equals> <one-optional-space> <token>

<shorthand-definition>
    \chardef <control-sequence> <equals> <8-bit-number>
    \mathchardef <control-sequence> <equals> <15-bit number>
    <registerdef> <control-sequence> <equals> <8-bit-number>

<registerdef>
    \countdef
    \dimendef
    \skipdef
    \muskipdef
    \toksdef

<family-assignment>
    <family-member> <equals> <font>

<shape-assignment>
    \parshape <equals> <number> <shape-dimensions>

### Global assignments

<global-assignment>
    <font-assignment>
    <hyphenation-assignment>
    <box-size-assignment>
    <interaction-mode-assignment>
    <intimate-assignment>

<font-assignment>
    \fontdimen <number> <font> <equals> <dimen>
    \hyphenchar <font> <equals> <number>
    \skewchar <font> <equals> <number>

<hyphenation-assignment>
    \hyphenation <general-text>
    \patterns <general-text>

<box-size-assignment>
    <box-dimension> <8-bit-number> <equals> <dimen>

<interaction-mode-assignment>
    \errorstopmode
    \scrollmode
    \nonstopmode
    \batchmode

<intimate-assignment>
    <special-integer> <equals> <number>
    <special-dimen> <equals> <dimen>

## Box specifications

<box>
    \box <8-bit-number>
    \copy <8-bit-number>
    \lastbox
    \vsplit <8-bit-number> [to] <dimen>
    \hbox <box-specification> { <horizontal mode material> }
    \vbox <box-specification> { <vertical mode material> }
    \vtop <box-specification> { <vertical mode material> }

<box-specification>
    [to] <dimen> <filler>
    [spread] <dimen> <filler>
    <filler>

<box-or-rule>
    <box>
    <vertical-rule>
    <horizontal-rule>

<vertical-rule>
    \vrule <rule-specification>

<horizontal-rule>
    \hrule <rule-specification>

<rule-specification>
    <optional-spaces>
    <rule-dimension> <rule-specification>

<rule-dimension>
    [width] <dimen>
    [height] <dimen>
    [depth] <dimen>

## Commands

<optional-immediate>
    <empty>
    \immediate

<start-par>
    \indent
    \noindent

<all-modes-command>
    <assignment>
    \relax
    }
    \begingroup
    \endgroup
    \show <token>
    \showbox <8-bit-number>
    \showlists
    \showthe <internal-quantity>
    \shipout <box>
    \ignorespaces <optional-spaces>
    \afterassignment <token>
    \aftergroup <token>
    \uppercase <general-text>
    \lowercase <general-text>
    \message <general-text>
    \errmessage <general-text>
    \openin <4-bit-number> <equals> <file-name>
    \closein <4-bit-number>
    % (list-dependent commands)
    <optional-immediate> \openout <4-bit-number> <equals> <file-name>
    <optional-immediate> \closout <4-bit-number>
    <optional-immediate> \write <number> <general-text>
    \special <general-text>
    \penalty <number>
    \kern <dimen>
    \mkern <mudimen>
    \unpenalty
    \unkern
    \unskip
    \mark <general-text>
    \insert <8-bit-number> <filler> { <vertical-mode-material> }
    \vadjust <filler> { <vertical-mode-material> }
    % Per-mode in Texbook, but grammatically all-mode.
    <space-token>
    <box>
    <start-par>
    \par
    {

## Commands in vertical mode

<vertical-skip>
    \vskip <glue>
    \vfil
    \vfill
    \vss
    \vfilneg

<move-v-box>
    \moveleft
    \moveright

<get-v-box>
    \unvbox
    \unvcopy

<enter-h-mode-command>
    <letter>
    <otherchar>
    \char
    <chardef token>
    <get-h-box>
    \valign
    \vrule
    <horizontal-skip>
    \accent
    \discretionary
    \-
    \ 
    $

<leaders>
    \leaders
    \cleaders
    \xleaders

<v-mode-command>
    <all-modes-command>
    <vertical-skip>
    <leaders> <box-or-rule> <vertical-skip>
    <move-v-box> <dimen> <box>
    <get-v-box> <8-bit-number>
    <horizontal-rule>
    \halign <box-specification> { <alignment-material> }
    <enter-h-mode-command>
    \end
    \dump

## Commands in Horizontal mode

<horizontal-skip>
    \hskip
    \hfil
    \hfill
    \hss
    \hfilneg

<move-h-box>
    \raise
    \lower

<get-h-box>
    \unhbox
    \unhcopy

<leave-h-mode-command>
    <get-v-box>
    \halign
    \hrule
    <vertical-skip>
    \end
    \dump

<h-mode-command>
    <all-modes-command>
    <horizontal-skip>
    <leaders> <box-or-rule> <horizontal-skip>
    \ 
    <move-h-box> <dimen> <box>
    <get-h-box>
    <vertical-rule>
    \valign <box-specification> { <alignment-material> }
    <leave-h-mode-command>
    <letter>
    <otherchar>
    \char <8-bit-number>
    <chardef-token>
    \accent <8-bit-number> <optional-assignments>
    \/
    \discretionary <general-text> <general-text> <general-text>
    \-
    $

